# Copyright The OpenTelemetry Authors
# SPDX-License-Identifier: Apache-2.0

load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load(
    "//:config.bzl",
    "disabled_setting_substitution",
    "enabled_setting_substitution",
    "get_substitutions",
)

package(features = ["no_copts_tokenization"])

bool_flag(
    name = "use_mbedtls",
    build_setting_default = False,
)

config_setting(
    name = "use_mbedtls_setting",
    flag_values = {":use_mbedtls": "true"},
)

bool_flag(
    name = "http_only",
    build_setting_default = True,
)

config_setting(
    name = "http_only_setting",
    flag_values = {":http_only": "true"},
)

expand_template(
    name = "curl_config",
    out = "lib/curl_config.h",
    substitutions =
        get_substitutions() |
        select({
            ":use_mbedtls_setting": dict([enabled_setting_substitution("USE_MBEDTLS")]),
            "//conditions:default": dict([disabled_setting_substitution("USE_MBEDTLS")]),
        }),
    template = "lib/curl_config.h.cmake",
)

cc_library(
    name = "curl",
    srcs = [
        ":curl_config",
    ] + glob([
        "lib/**/*.c",
        "lib/**/*.h",
    ]),
    hdrs = glob(["include/curl/*.h"]),
    defines = [
        "CURL_STATICLIB",
    ],
    includes = [
        "include",
        "lib",
    ],
    linkopts = select({
        "@platforms//os:windows": [
            "-DEFAULTLIB:ws2_32.lib",
            "-DEFAULTLIB:advapi32.lib",
            "-DEFAULTLIB:crypt32.lib",
            "-DEFAULTLIB:Normaliz.lib",
        ],
        "@platforms//os:macos": [
            "-Wl,-framework,SystemConfiguration",
            "-lpthread",
        ],
        "//conditions:default": [
            "-lpthread",
        ],
    }),
    local_defines = [
        "BUILDING_LIBCURL",
        "HAVE_CONFIG_H",
    ] + select({
        ":http_only_setting": ["HTTP_ONLY"],
        "//conditions:default": [],
    }) + select({
        "@platforms//os:windows": [
            "WIN32",
        ],
        "//conditions:default": [
            "CURL_HIDDEN_SYMBOLS",
        ],
    }),
    visibility = ["//visibility:public"],
    deps = select({
        ":use_mbedtls_setting": ["@mbedtls"],
        "//conditions:default": [],
    }),
)

cc_binary(
    name = "linktest",
    linkopts = select({
        "@platforms//os:macos": ["-all_load"],
        "//conditions:default": [],
    }),
    linkshared = True,
    visibility = ["//visibility:private"],
    deps = [":curl"],
)
